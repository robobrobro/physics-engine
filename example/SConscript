from SCons.Errors import StopError
import os
import platform

Import('env')

OS = platform.system().lower()
GLFW_PATH = os.environ.get('GLFW_PATH', '')

env.Append(
    CPPPATH = [
        os.path.join(GLFW_PATH, 'include'),
    ],
    LIBPATH = [
        os.path.join(GLFW_PATH, 'src'),
    ],
    LIBS = [
        'glfw3',
        'physics_engine',
    ],
)

if OS == 'darwin':
    env.Append(
        LINKFLAGS = [
            '-framework', 'Cocoa',
            '-framework', 'CoreVideo',
            '-framework', 'IOKit',
            '-framework', 'OpenGL',
        ],
    )
elif 'cygwin' in OS:
    env.Append(
        LIBS = [
            'opengl32',
            'X11',
        ],
    )

target = 'example'
source = (
    'src/main.c',
)

example = env.Program(target, source)

Return('example')
